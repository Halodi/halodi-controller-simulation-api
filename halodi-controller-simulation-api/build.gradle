buildscript {
    repositories {
       mavenCentral()
       maven { url "https://plugins.gradle.org/m2/" }
       mavenLocal()
       jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
        classpath 'net.saliman:gradle-properties-plugin:1.5.1'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.saliman.properties'

def vcsurl = 'https://github.com/Halodi/halodi-controller-simulation-api.git'
group = "com.halodi"
def inputVersion= project.hasProperty("artifactVersion") ? 
                project.getProperty("artifactVersion"): System.getenv('ARTIFACT_VERSION')
version = "$inputVersion$versionAppendix"
def repoUrl = "https://artifacts.halodi.com/repository"


dependencies {
   implementation 'org.apache.commons:commons-lang3:3.4'
   implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.7'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Jesper Smith"
            email "jesper@halodi.com"
        }
    }
    
    scm {
       url vcsurl
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        JavaPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId project.name
            version version
        }
    }
}
 

publishing {
    repositories {
        maven {
            def inputUsername = project.hasProperty("artifactUsername") ? 
                project.getProperty("artifactUsername"): System.getenv('ARTIFACT_USERNAME')
            def inputPassword = project.hasProperty("artifactPassword") ? 
                project.getProperty("artifactPassword"): System.getenv('ARTIFACT_PASSWORD')
            credentials {
                username inputUsername
                password inputPassword
            }
            url "$repoUrl$repository"
        }
    }
}